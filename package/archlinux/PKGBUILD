# Author: mosra <mosra@centrum.cz>
pkgname=magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine"
arch=('i686' 'x86_64')
url="https://magnum.graphics"
license=('MIT')
depends=('magnum')
makedepends=('cmake' 'ninja')
options=(!strip)
provides=('magnum-extras-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build"
    cd "$_rootdir/build"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DMAGNUM_BUILD_TESTS=ON \
        -DMAGNUM_BUILD_GL_TESTS=ON \
        -DMAGNUM_WITH_PLAYER=ON \
        -DMAGNUM_WITH_UI=ON \
        -DMAGNUM_WITH_UI_GALLERY=ON \
        -G Ninja
    ninja
}

check() {
    cd "$_rootdir/build"
    ctest --output-on-failure
}

package() {
    cd "$_rootdir/build"
    DESTDIR="$pkgdir/" ninja install
}
