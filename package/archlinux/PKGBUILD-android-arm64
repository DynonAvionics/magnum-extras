# Author: mosra <mosra@centrum.cz>
pkgname=android-arm64-magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (Android ARM64)"
arch=('any')
url="https://magnum.graphics"
license=('MIT')
depends=('android-arm64-magnum')
makedepends=('cmake' 'ninja' 'android-ndk' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-arm64" ] ; then
        mkdir "$_rootdir/build-android-arm64"
        cd "$_rootdir/build-android-arm64"

        cmake .. \
            -DCMAKE_SYSTEM_NAME=Android \
            -DCMAKE_SYSTEM_VERSION=22 \
            -DCMAKE_ANDROID_ARCH_ABI=arm64-v8a \
            -DCMAKE_ANDROID_NDK_TOOLCHAIN_VERSION=clang \
            -DCMAKE_ANDROID_STL_TYPE=c++_static \
            -G Ninja
    fi

    cd "$_rootdir/build-android-arm64"

    # Colored output is enabled implicitly. If Ninja detects it's outputting to
    # a pipe, it strips it away from the output; Clang unfortunately doesn't
    # have any env var to temporarily disable colors. The inverse, i.e.
    # preserving colors when Ninja outputs to a pipe can be done with
    # CLICOLOR_FORCE=1: https://github.com/ninja-build/ninja/issues/2196
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-22/arch-arm64/usr \
        -DCMAKE_COLOR_DIAGNOSTICS=ON \
        -DMAGNUM_INCLUDE_INSTALL_PREFIX=/opt/android-ndk/sysroot/usr \
        -DMAGNUM_WITH_PLAYER=OFF \
        -DMAGNUM_WITH_UI=ON \
        -DMAGNUM_WITH_UI_GALLERY=OFF \
        -DMAGNUM_BUILD_TESTS=ON \
        -DMAGNUM_BUILD_GL_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-arm64"
    DESTDIR="$pkgdir/" ninja install/strip
}
