# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum-extras
pkgver=dev.webgl2
pkgrel=1
pkgdesc="Extras for Magnum OpenGL graphics engine (Emscripten, WebGL 2)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('emscripten-magnum')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-noopt-webgl2" ] ; then
        mkdir "$_rootdir/build-emscripten-noopt-webgl2"
        cd "$_rootdir/build-emscripten-noopt-webgl2"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-noopt-webgl2"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DMAGNUM_DEPLOY_PREFIX=/srv/http/emscripten/magnum \
        -DCMAKE_CXX_FLAGS_RELEASE="-DNDEBUG -O1" \
        -DCMAKE_EXE_LINKER_FLAGS_RELEASE="-O1" \
        -DWITH_UI=ON \
        -DWITH_UI_GALLERY=ON
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-noopt-webgl2"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-noopt-webgl2"
    DESTDIR="$pkgdir/" ninja install
}
